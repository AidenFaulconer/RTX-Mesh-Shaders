#version 450
#pragma shader_stage(task)

#define USE_NATIVE   1
#extension GL_NV_mesh_shader : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int8 : enable
#extension GL_KHR_shader_subgroup_basic : require
#extension GL_KHR_shader_subgroup_ballot : require
#extension GL_KHR_shader_subgroup_vote : require

#define GROUP_SIZE 32

layout(local_size_x=GROUP_SIZE) in;

layout(push_constant) uniform PushConstants {
    uint batch_size;
    uint num_meshlets;
} drawcall_info;

taskNV out Task
{
	uint base_id;
	uint8_t sub_ids[GROUP_SIZE];
	uint num_meshlets;
} OUT;

void main()
{
	const uint base_id = gl_WorkGroupID.x * GROUP_SIZE;
	const uint lane_id = gl_LocalInvocationID.x;
	const uint global_id = gl_GlobalInvocationID.x;
	const uint total_meshlet_count = subgroupBroadcastFirst(drawcall_info.num_meshlets * drawcall_info.batch_size);

	bool render = !(global_id > total_meshlet_count);
	uvec4 vote = subgroupBallot(render);
	uint tasks = subgroupBallotBitCount(vote);

	if (lane_id == 0)
	{
		gl_TaskCountNV = tasks;
		OUT.base_id = base_id;
		OUT.num_meshlets = drawcall_info.num_meshlets;
	}

	uint idx_offset = subgroupBallotExclusiveBitCount(vote);
	if (render)
	{
		OUT.sub_ids[idx_offset] = uint8_t(lane_id);
	}
}
